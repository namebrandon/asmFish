
Gen_Evasions:
	; in x20 address of position
	;    x21 address of state
	; io x27 address to write moves

            stp  x29, x30, [sp, -16]!

            ldr  w13, [x20, Pos.sideToMove]
            ldr  x14, [x20, Pos.typeBB + 8*King]
            add  x6, x20, Pos.typeBB
            ldr  x4, [x6, x13, lsl 3]
            and  x14, x14, x4
           rbit  x14, x14
            clz  x14, x14

            ldr  x16, [x20, Pos.typeBB + 8*Pawn]
            ldr  x4, [x20, Pos.typeBB + 8*Knight]
            orr  x16, x16, x4
            ldr  x4, [x21, State.checkersBB]
            bic  x16, x4, x16

            lsl  x9, x14, 9
            mov  x12, 0
           rbit  x2, x16
            clz  x2, x2
            cbz  x16, .SlidersDone

.NextSlider:
            sub  x8, x16, 1
            and  x16, x16, x8
           _lea  x7, LineBB
            add  x7, x7, x9
            ldr  x0, [x7, x2, lsl 3]
            mov  x4, 1
            lsl  x4, x4, x2
            bic  x0, x0, x4
            orr  x12, x12, x0
           rbit  x2, x16
            clz  x2, x2
           cbnz  x16, .NextSlider
        
.SlidersDone:
    ; generate moves for the king to safe squares
            add  x6, x20, Pos.typeBB
            ldr  x16, [x6, x13, lsl 3]
           _lea  x7, KingAttacks
            ldr  x4,  [x7, x14, lsl 3]
            bic  x16, x4, x16
            bic  x12, x16, x12
            lsl  x14, x14, 6
           rbit  x0, x12
            cbz  x12, .KingMoveDone
.NextKingMove:
            clz  x0, x0
            sub  x8, x12, 1
            and  x12, x12, x8
            orr  x0, x0, x14
            str  w0, [x27], sizeof.ExtMove
           rbit  x0, x12
           cbnz  x12, .NextKingMove
.KingMoveDone:
    ; if there are multiple checkers, only king moves can be evasions
            ldr  x1, [x21, State.checkersBB]
            sub  x0, x1, 1
            tst  x0, x1
            bne  Gen_Evasions_White.Return
           rbit  x0, x1
            clz  x0, x0
           _lea  x7, BetweenBB
            add  x7, x7, x9
            ldr  x15, [x7, x0, lsl 3]
            orr  x15, x15, x1
            ldr  x14, [x20, Pos.typeBB + 8*White]
            ldr  x4, [x20, Pos.typeBB + 8*Black]
            orr  x14, x14, x4
           cbnz  w13, Gen_Evasions_Black

Gen_Evasions_White:
         GenAll  White, EVASIONS
.Return:
            ldp  x29, x30, [sp], 16
            ret
     GenPawnJmp  White, EVASIONS
 GenCastlingJmp  White, EVASIONS

Gen_Evasions_Black:
         GenAll  Black, EVASIONS
            ldp  x29, x30, [sp], 16
            ret
     GenPawnJmp  Black, EVASIONS
 GenCastlingJmp  Black, EVASIONS

