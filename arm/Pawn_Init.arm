Pawn_Init:
           _lea  x1, Connected
            adr  x7, .Seed
            add  x1, x1, 4
            mov  w2, 0
            mov  w13, 2
            mov  w12, 4
.opposedLoop:
            mov  x8, x1
            mov  w3, 0
.phalanxLoop:
            mov  x9, x8
            mov  w4, 0
.supportLoop:
            add  x6, x7, 4
            mov  x11, x9
            mov  w5, -1
.rankLoop:
            mov  w0, 0
            ldr  w10, [x6]
            cbz  w3, .phalanxIsZero
            ldr  w0, [x6, 4]
            sub  w0, w0, w10
           sdiv  w0, w0, w13
.phalanxIsZero:
            add  w0, w0, w10
            add  x6, x6, 4
            asr  w0, w0, w2
            add  w0, w0, w4
            mul  w10, w5, w0
            add  w5, w5, 1
            cmp  w5, 5
           sdiv  w10, w10, w12
            add  w0, w10, w0, lsl 16
            str  w0, [x11], 4
            bne  .rankLoop
            add  w4, w4, 17
            add  x9, x9, 32
            cmp  w4, 51
            bne  .supportLoop
            add  w3, w3, 1
            add  x8, x8, 96
            cmp  w3, 2
            bne  .phalanxLoop
            add  w2, w2, 1
            add  x1, x1, 192
            cmp  w2, 2
            bne  .opposedLoop
            ret

.Seed:
        dd  0, 13, 24, 18, 76, 100, 175, 330
