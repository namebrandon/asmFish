
macro SliderBlockers result, sliders, sq, pinners,\
                     pieces, pieces_color,\
                     bb, snipers, snipersSqBB, tt, bishops, rooks, temp
  local YesPinners, NoPinners, MoreThanOne

            ldr  snipers, [x20, Pos.typeBB + 8*Queen]
            mov  bb, snipers

            orr  snipers, snipers, rooks
           _lea  temp, RookAttacksPDEP
            ldr  tt, [temp, sq, lsl 3]
            and  snipers, snipers, tt

            orr  bb, bb, bishops
           _lea  temp, BishopAttacksPDEP
            ldr  tt, [temp, sq, lsl 3]
            and  bb, bb, tt

            orr  snipers, snipers, bb

           _lea  tt, BetweenBB
            add  sq, tt, sq, lsl 9
           ands  snipers, snipers, sliders
            beq  NoPinners

YesPinners:
           rbit  tt, snipers
            neg  snipersSqBB, snipers
            clz  tt, tt
            and  snipersSqBB, snipersSqBB, snipers
            ldr  bb, [sq, tt, lsl 3]
            and  bb, bb, pieces
           ands  tt, bb, pieces_color
           csel  snipersSqBB, tt, snipersSqBB, eq
            sub  tt, bb, 1
            and  tt, tt, bb
           cbnz  tt, MoreThanOne
            orr  result, result, bb
            orr  pinners, pinners, snipersSqBB
MoreThanOne:
            sub  tt, snipers, 1
            and  snipers, snipers, tt
           cbnz  snipers, YesPinners
NoPinners:
end macro


